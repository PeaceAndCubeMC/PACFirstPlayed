plugins {
    id 'java'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

group = project.pacfirstplayed_group
archivesBaseName = project.pacfirstplayed_name
version = project.pacfirstplayed_version

repositories {
    // Paper
    maven { url "https://papermc.io/repo/repository/maven-public/" }
}

configurations {
    include.canBeResolved = true
    compileOnly.extendsFrom(include)
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paper_version}"
}

build {
    dependsOn(shadowJar)
}

shadowJar {
    configurations = [project.configurations.include]

    archiveFileName = "${archivesBaseName}-${version}.jar"
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

processResources {
    filesMatching('plugin.yml') {
        expand(
                version: version
        )
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/PeaceAndCubeMC/PACFirstPlayed"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
